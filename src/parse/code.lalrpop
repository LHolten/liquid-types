use std::rc::Rc;
use crate::parse::expr::*;
use crate::parse::types::*;
use crate::parse::lexer::*;

grammar;

Num: i32 = "num" => <>;
Var: String = "var" => <>;

Value: Value = {
    "(" <UnboxedValue> ")" => <>,
    <Spanned<Var>> <Index*> => Value::Var(<>),
    Num => Value::Int32(<>),
}

UnboxedValue: Value = {
    Value => <>,
    BinOpValue => Value::BinOp(Box::new(<>)),
    Prop => Value::Prop(Box::new(<>)),
};

Index: Index = {
    "." <Spanned<Var>> => Index::Attribute(<>),
    "[" <idx:UnboxedValue> "]" => Index::Value(<>),
};

BinOpValue: BinOpValue = <l:Value> <op:BinOp> <r:Value> => BinOpValue{<>};
BinOp: BinOp = {
    "+" => BinOp::Plus,
    "-" => BinOp::Minus,
    "*" => BinOp::Times,
    "%" => BinOp::Modulo,
    "/" => BinOp::Divide,
    "<<" => BinOp::Shl,
    ">>" => BinOp::Shr,
};

Tuple: Vec<Value> = "(" <first:(<UnboxedValue> ",")*> <last:UnboxedValue?> ")" => {
    let mut all = first;
    all.extend(last);
    all
};
Bind: Bind = <func:Spanned<Var>?> <args:Spanned<Tuple>> => Bind{<>};

Let: Let = "let" "(" <names:(<Var> ","?)*> ")" "=" <bind:Bind> => Let{<>};

If: If = "if" <val:UnboxedValue> "{" <block:Block> "}" => If{<>};

Stmt: Stmt = {
    Let => Stmt::Let(<>),
    "#debug" => Stmt::Debug,
    FuncDef => Stmt::FuncDef(<>),
    If => Stmt::If(<>),
};

BlockInner: Block = {
    <step:Spanned<Stmt>> ";" <next:Block> => Block::Stmt{<>},
    "return" <end:Bind> => Block::End(<>),
};
Block: Rc<Spanned<Block>> = Spanned<BlockInner> => Rc::new(<>);

FuncDef: FuncDef = {
    "fn" <name: Var> <typ:NegTyp> "=" "{" <block:Block> "}" => FuncDef{<>},
    "loop" <name: Var> <typ:PosTyp> "=" "{" <block:Block> "}" => FuncDef{
        name,
        typ: NegTyp {
            args: typ.clone(),
            ret: typ,
        },
        block,
    }
};

pub NegTyp: NegTyp = <args:PosTyp> "->" <ret:PosTyp> => NegTyp{<>};

PosTypInner: PosTyp = "(" <names:(<Var> ","?)*> ")" <parts:("where" "{" <(<Spanned<Constraint>> ";")*> "}")?> => {
    let parts = parts.into_iter().flatten().collect();    
    PosTyp{names, parts}
};
PosTyp: Rc<Spanned<PosTyp>> = Spanned<PosTypInner> => Rc::new(<>);

Forall: Forall = "[" <names:(<Var> ","?)*> "]" "=" <named:Spanned<Var>> "if" <cond:Prop> => Forall{named, names, cond: Rc::new(cond)};
Switch: Switch = <named:Spanned<Var>> <args:Tuple> <cond:("if" <UnboxedValue>)?> => Switch{<>};

Constraint: Constraint = {
    Forall => Constraint::Forall(<>),
    <(<Var> "=")?> <Switch> => Constraint::Switch(<>),
    "let" <Var> "=" <UnboxedValue> => Constraint::Let(<>),
    <Spanned<Var>> => Constraint::Exactly(<>),
    "assert" <Prop> => Constraint::Assert(<>),
};

Prop: Prop = <l:Value> <op:PropOp> <r:Value> => Prop{<>};
PropOp: PropOp = {
    "<" => PropOp::Less,
    "<=" => PropOp::LessEq,
    "==" => PropOp::Eq,
    "!=" => PropOp::NotEq,
    "&&" => PropOp::And,
    "||" => PropOp::Or,
    "*?" => PropOp::MulSafe,
    "+?" => PropOp::AddSafe,
};

NamedConstraint: NamedConstraint = "type" <name:Var> <typ:PosTyp> => NamedConstraint{<>};

Def: Def = {
    FuncDef => Def::Func(<>),
    NamedConstraint => Def::Typ(<>),
};

pub Module: Module = Def* => Module(<>);

Spanned<T>: Spanned<T> = {
    <l:@L> <val:T> <r:@R> => Spanned {span: (l, r - l).into(), val},
};


extern {
  type Location = usize;
  type Error = LexicalError;

  enum Token {
    "var" => Token::Var(<String>),
    "num" => Token::Num(<i32>),
    "=" => Token::Assign,
    ":" => Token::Colon,
    "->" => Token::Arrow,
    "." => Token::Period,
    "," => Token::Comma,
    ";" => Token::Semi,
    "+" => Token::Add,
    "-" => Token::Sub,
    "*" => Token::Mul,
    "%" => Token::Mod,
    "/" => Token::Div,
    "<<" => Token::Shl,
    ">>" => Token::Shr,
    "<" => Token::Less,
    "<=" => Token::LessEq,
    "==" => Token::Eq,
    "!=" => Token::NotEq,
    "&&" => Token::And,
    "||" => Token::Or,
    "*?" => Token::MulSafe,
    "+?" => Token::AddSafe,
    "{" => Token::BraceL,
    "}" => Token::BraceR,
    "(" => Token::ParenL,
    ")" => Token::ParenR,
    "[" => Token::SquareL,
    "]" => Token::SquareR,
    "fn" => Token::FnKeyword,
    "loop" => Token::LoopKeyword,
    "if" => Token::IfKeyword,
    "for" => Token::ForKeyword,
    "type" => Token::TypeKeyword,
    "where" => Token::WhereKeyword,
    "assert" => Token::AssertKeyword,
    "let" => Token::LetKeyword,
    "return" => Token::ReturnKeyword,
    "#debug" => Token::DebugKeyword,
  }
}