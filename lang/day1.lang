type array(from, len) where {
    @byte for (ptr) if (ptr - from) < len;
}

type arrayln(from, len) where {
    assert 1 <= len;
    move last = @byte(from + (len - 1));
    assert last == '\n';
    array(from, len - 1);
}

fn part1(len) where {
    arrayln(0, len);
} -> (out) where {
    arrayln(0, len);
} = {
    loop inner(sum, idx) where {
        assert idx <= len;
        arrayln(0, len);
    } = {
        if idx == len {
            return (sum, idx)
        };

        loop find_first(idx, prev) where {
            assert idx < len;
            arrayln(0, len);
        } = {
            let (val) = @read_u8(idx);
            if val == '\n' {
                return (idx, prev)
            };
            if ('0' <= val) && (val <= '9') {
                return (idx + 1, val - '0')
            };
            return find_first(idx + 1, prev)
        };

        loop find_last(idx, prev) where {
            assert idx < len;
            arrayln(0, len);
        } = {
            let (val) = @read_u8(idx);
            if val == '\n' {
                return (idx, prev)
            };
            if ('0' <= val) && (val <= '9') {
                return find_last(idx + 1, val - '0')
            };
            return find_last(idx + 1, prev)
        };

        let (idx, first) = find_first(idx, 0);
        let (idx, last) = find_last(idx, first);
        return inner(sum + ((10 * first) + last), idx + 1)
    };

    let (sum, end) = inner(0, 0);
    return (sum)
}